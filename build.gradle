buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1'
    }
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply from: 'artifactory-template.gradle'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url 'http://jartifacts.longlinkislong.com/artifactory/libs-snapshot' }
    maven { url 'http://jartifacts.longlinkislong.com/artifactory/libs-release' }    
}

def getSnapshot(projectName) {
    def result = new ProcessBuilder('sqlite3', database, "SELECT hash FROM versioning WHERE library='$projectName'").start().text.trim()
    
    return "$result-SNAPSHOT"
}

def getVersion(projectName) {
    return getVersion(projectName, build)
}

def getVersion(projectName, buildType) {
    if (isJenkins.toBoolean()) {
        if (buildType == 'snapshot') {
            return getSnapshot(projectName)
        } else if (buildType == 'release') {
            return getRelease(projectName)
        } else {
            throw new IllegalStateException("Unsupported build type: $buildType")
        }
    } else {
        if (buildType == 'snapshot') {
            return "http://jenkins.longlinkislong.com/job/$projectName/lastSuccessfulBuild/artifact/build/version.snapshot".toURL().text.trim()
        } else if (buildType == 'release') {
            return "http://jenkins.longlinkislong.com/job/$projectName/lastSuccessfulBuild/artifact/build/version.release".toURL().text.trim()
        } else {
            throw new IllegalArgumentException("Unsupported build type: $buildType")
        }
    }
}

def getRelease(projectName) {
    def version = new ProcessBuilder('sqlite3', database, "SELECT major,minor,revision FROM versioning WHERE library='$projectName'").start().text.trim()
    
    if (version.empty) {
        def dbFile = new File(database).absolutePath
        System.err.println("Library [$projectName] was not found in database: $dbFile")
        return null
    } else {
        return version.replace('|', '.')
    }
}

dependencies {        
    compile 'org.slf4j:slf4j-api:1.7.+'
    compile "com.longlinkislong:gloop:${getVersion('gloop')}"
    compile "com.longlinkislong:gloop-window-glfw:${getVersion('gloop-window-glfw')}"
    compile "com.runouw:wrappers:${getVersion('wrappers')}"
    
    
    compile files("${System.properties['java.home']}/../lib/tools.jar")
    testCompile 'junit:junit:4.10'
    testCompile 'org.slf4j:slf4j-simple:1.7.+'
    testCompile "com.longlinkislong:gloop-window-glfw:${getVersion('gloop-window-glfw')}"
    testCompile "com.longlinkislong:gloop-impl-opengl:${getVersion('gloop-impl-opengl')}"
}

compileJava.options.compilerArgs = ['-Xlint:all']
